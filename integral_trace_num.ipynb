{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "85c852d0",
   "metadata": {},
   "outputs": [],
   "source": [
    "%load_ext cython"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "94f8aecb",
   "metadata": {},
   "source": [
    "We check numerically that $e\\in \\mathbb{S}^{n-1}$ and $A\\in \\mathbb{R}^{n\\times n}$ be arbitrary.\n",
    "Then\n",
    "$$\n",
    "  |\\mathbb{S}^{n-1}|^{-1}\n",
    "  \\int_{\\mathbb{S}^{n-1}}\n",
    "  \\left|e^T s\\right|^{p-2}\n",
    "  s^T\\left[(p-1)I  + (2-p)ee^T\\right]As\n",
    "  \\,\\mathrm{d}s = K_{n,p} \\mathrm{trace} A,\n",
    "$$\n",
    "where\n",
    "$$\n",
    "    K_{p,n}=\n",
    "  |\\mathbb{S}^{n-1}|^{-1}\n",
    "  \\int_{\\mathbb{S}^{n-1}}\n",
    "  \\left|e^T s\\right|^{p}\n",
    "  \\,\\mathrm{d}s.\n",
    "$$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "2cb41fe3",
   "metadata": {},
   "outputs": [],
   "source": [
    "%%cython -f -c=-O3 -c=-Wno-deprecated-declarations -c=-Wno-unreachable-code-fallthrough\n",
    "#-a\n",
    "\n",
    "from cpython.pycapsule cimport (PyCapsule_New,\n",
    "                                PyCapsule_GetPointer)\n",
    "from cpython.mem cimport PyMem_Malloc,  PyMem_Free\n",
    "from libc.math cimport sin, cos, pow, abs\n",
    "from libc.stdlib cimport malloc, free\n",
    "import scipy\n",
    "from cython cimport boundscheck, wraparound, cdivision, nonecheck\n",
    "\n",
    "@boundscheck(False)\n",
    "@wraparound(False)\n",
    "@nonecheck(False)\n",
    "@cdivision(True)\n",
    "cdef double sph_jac(int n, double* phi):\n",
    "    \"\"\"Jacobian of the spherical coordinates\"\"\"\n",
    "    cdef int i\n",
    "    jac = 1.0\n",
    "    for i in range(n-2):\n",
    "        jac *= pow(sin(phi[i]),n-2-i)\n",
    "    return jac\n",
    "\n",
    "@boundscheck(False)\n",
    "@wraparound(False)\n",
    "@nonecheck(False)\n",
    "@cdivision(True)\n",
    "cdef void sph2cart(int n, double* phi, double* z):\n",
    "    \"\"\"Convert spherical to cartezian coordinates\"\"\"\n",
    "    cdef double sin_prod = 1.0\n",
    "    cdef int i\n",
    "    for i in range(n-1):\n",
    "        z[i] = sin_prod*cos(phi[i])\n",
    "        sin_prod *= sin(phi[i])\n",
    "    z[n-1] = sin_prod\n",
    "\n",
    "@boundscheck(False)\n",
    "@wraparound(False)\n",
    "@nonecheck(False)\n",
    "@cdivision(True)\n",
    "cdef double integrand_main(int N, double* phi, void* user_data):\n",
    "    \"\"\"The integrand, written in Cython\"\"\"\n",
    "    # Extract the value of p, vector e, and matrix A\n",
    "    # Cython uses array access syntax for pointer dereferencing!\n",
    "    cdef int     n = N+1\n",
    "    cdef double  p = (<double*>user_data)[0]\n",
    "    cdef double* e = (<double*>user_data)+1\n",
    "    cdef double* A = (<double*>user_data)+1+n\n",
    "    cdef double* z = (<double*>user_data)+1+n+n*n\n",
    "    cdef int i, j\n",
    "    cdef double e_dot_z, e_dot_z_abs, z_A_z, e_A_z, A_z_i\n",
    "    #\n",
    "    # first, we need to transform from spherical to Cartesian coordinates\n",
    "    sph2cart(n, phi, z)\n",
    "    # then we can evaluate the integrand\n",
    "    e_dot_z = 0.0\n",
    "    z_A_z   = 0.0\n",
    "    e_A_z   = 0.0\n",
    "    for i in range(n):\n",
    "        A_z_i = 0.0\n",
    "        for j in range(n):\n",
    "            A_z_i += A[i*n+j]*z[j]\n",
    "        e_dot_z += e[i]*z[i]\n",
    "        z_A_z   += z[i]*A_z_i\n",
    "        e_A_z   += e[i]*A_z_i\n",
    "    e_dot_z_abs = abs(e_dot_z)\n",
    "    if (p>=2) or (e_dot_z_abs>1.0E-12):\n",
    "        return pow(e_dot_z_abs,p-2)*((p-1)*z_A_z + (2-p)*e_dot_z*e_A_z) * sph_jac(n, phi)\n",
    "    else:\n",
    "        return 0.0\n",
    "\n",
    "#\n",
    "# Pack numpy arrays containing the parameters of the integrand\n",
    "#\n",
    "cdef object pack_user_data(int n, double p, double[:] e, double[:,:] A):\n",
    "    \"\"\"Wrap data in a PyCapsule for transport.\"\"\"\n",
    "    # Allocate memory\n",
    "    cdef double* data_ptr = <double*> PyMem_Malloc(sizeof(double)*(1+n+n*n+n))\n",
    "    data_ptr[0] = p\n",
    "    cdef int i,j\n",
    "    for i in range(n):\n",
    "        data_ptr[1+i] = e[i]\n",
    "    for i in range(n):\n",
    "        for j in range (n):\n",
    "            data_ptr[1+n+i*n+j] = A[i,j]\n",
    "    # the last n doubles is a buffer\n",
    "    return PyCapsule_New(<void*>data_ptr, NULL, free_user_data)\n",
    "\n",
    "cdef void free_user_data(capsule):\n",
    "    \"\"\"Free the memory our value is using up.\"\"\"\n",
    "    PyMem_Free(PyCapsule_GetPointer(capsule, NULL))\n",
    "\n",
    "    \n",
    "def get_low_level_callable_main(int n, double p, double[:] e, double[:,:] A):\n",
    "    # scipy.LowLevelCallable expects the function signature to\n",
    "    # appear as the \"name\" of the capsule\n",
    "    func_capsule = PyCapsule_New(<void*>integrand_main,\n",
    "                                 \"double (int, double *, void *)\",\n",
    "                                 NULL)\n",
    "    data_capsule = pack_user_data(n, p, e, A)\n",
    "    \n",
    "    return scipy.LowLevelCallable(func_capsule, data_capsule)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "64040b67",
   "metadata": {},
   "outputs": [],
   "source": [
    "import numpy as np\n",
    "from scipy import integrate, special"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "b6cebb2b",
   "metadata": {},
   "outputs": [],
   "source": [
    "def compute_integrals(n,p,e,A):\n",
    "    ranges = [[0,np.pi]]*(n-2)\n",
    "    ranges.append([0,2*np.pi])\n",
    "    # compute the area of the sphere first\n",
    "    Sn  = 2*pow(np.pi,n/2) / special.gamma(n/2)\n",
    "    # compute Kpn\n",
    "    Kpn = pow(np.pi,-0.5) * special.gamma(n/2) \\\n",
    "          * special.gamma((p+1)/2) / special.gamma((n+p)/2)    \n",
    "    # the main integral\n",
    "    integrand_main = get_low_level_callable_main(n,p,e,A)\n",
    "    I, abserr      = integrate.nquad(integrand_main, ranges)\n",
    "    return I/Sn/Kpn"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "216da86e",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Integral over the sphere, I = 1.300119\n",
      "Trace of A,           tr(A) = 1.300119\n",
      "CPU times: user 19.7 s, sys: 301 ms, total: 20 s\n",
      "Wall time: 20 s\n"
     ]
    }
   ],
   "source": [
    "%%time\n",
    "\n",
    "p = 1.75\n",
    "n = 4\n",
    "\n",
    "rng = np.random.default_rng()\n",
    "\n",
    "A = rng.normal(size=[n,n])\n",
    "v = rng.normal(size=[n])\n",
    "e = v/np.linalg.norm(v)\n",
    "\n",
    "I = compute_integrals(n,p,e,A)\n",
    "\n",
    "\n",
    "print(\"Integral over the sphere, I = %f\" % I)\n",
    "print(\"Trace of A,           tr(A) = %f\" % np.trace(A))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "2229d53b",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3 (ipykernel)",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.8.16"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
